plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'com.palantir.git-version' version '0.13.0'
	id 'java'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.google.code.gson:gson:2.9.0'
	implementation 'org.ow2.asm:asm:9.2'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

test {
	useJUnitPlatform()
}

sourceCompatibility = 17

def buildNumber = System.getenv('AI_BUILD_NUMBER')
def gitInfo = versionDetails()
version = System.getenv('AI_VERSION_NUMBER') ?: 'snapshot'
if (!gitInfo.isCleanTag) version += '.dirty'

jar {
	manifest {
		attributes (
			'Implementation-Title': project.name,
			'Implementation-Version': project.version,
			'Implementation-Vendor': 'YuxuanZuo',
			'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			'Automatic-Module-Name': 'xyz.zuoyx.multiyggdrasil',
			'Premain-Class': 'xyz.zuoyx.multiyggdrasil.Premain',
			'Agent-Class': 'xyz.zuoyx.multiyggdrasil.Premain',
			'Can-Retransform-Classes': true,
			'Can-Redefine-Classes': true,
			'Git-Commit': gitInfo.gitHashFull,
			'Git-IsClean': gitInfo.isCleanTag,
			'Build-Number': buildNumber?:'-1'
		)
	}
}

processResources {
	from('LICENSE') {
		rename('LICENSE', 'multiyggdrasil.txt')
		into 'META-INF/licenses'
	}
}

shadowJar {
	exclude 'META-INF/maven/**'
	exclude 'module-info.class'
	exclude '**/module-info.class'

	relocate 'com.google.gson', 'xyz.zuoyx.multiyggdrasil.internal.com.google.gson'
	relocate 'org.objectweb.asm', 'xyz.zuoyx.multiyggdrasil.internal.org.objectweb.asm'
}

defaultTasks 'clean', 'shadowJar'
