plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'com.palantir.git-version' version '3.0.0'
	id 'java'
}

repositories {
	mavenCentral()
}

dependencies {
  implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'org.ow2.asm:asm:9.6'
}

test {
	useJUnitPlatform()
}

compileJava {
  options.release = 8
  options.deprecation = true
}

def buildNumber = System.getenv('AI_BUILD_NUMBER')
def gitInfo = versionDetails()
version = System.getenv('AI_VERSION_NUMBER') ?: 'snapshot'
if (!gitInfo.isCleanTag) version += '.dirty'

jar {
	manifest {
		attributes (
			'Implementation-Title': project.name,
			'Implementation-Version': project.version,
			'Implementation-Vendor': 'YuxuanZuo',
			'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			'Automatic-Module-Name': 'xyz.zuoyx.multiyggdrasil',
			'Premain-Class': 'xyz.zuoyx.multiyggdrasil.Premain',
			'Agent-Class': 'xyz.zuoyx.multiyggdrasil.Premain',
			'Can-Retransform-Classes': true,
			'Can-Redefine-Classes': true,
			'Git-Commit': gitInfo.gitHashFull,
			'Git-IsClean': gitInfo.isCleanTag,
			'Build-Number': buildNumber?:'-1'
		)
	}
}

processResources {
	from('LICENSE') {
		rename('LICENSE', 'multiyggdrasil.txt')
		into 'META-INF/licenses'
	}
}

shadowJar {
  archiveClassifier.set(null)

  exclude 'META-INF/maven/**'
  exclude 'module-info.class'
  exclude '**/module-info.class'

  relocate 'com.google.code', 'xyz.zuoyx.multiyggdrasil.internal.com.google.code'
  relocate 'org.objectweb.asm', 'xyz.zuoyx.multiyggdrasil.internal.org.objectweb.asm'
}

defaultTasks 'clean', 'shadowJar'
